buildscript {
    ext {
        gradlePluginVersion = '3.3.1'
        kotlinVersion = '1.3.11'
        kotlinCoroutinesVersion = '1.1.0'
        supportLibVersion = '27.1.1'
        tagSoupVersion = '1.2.1'
        glideVersion = '3.7.0'
        picassoVersion = '2.5.2'
        robolectricVersion = '3.5.1'
        jUnitVersion = '4.12'
        jSoupVersion = '1.11.3'
        wordpressUtilsVersion = '1.21'
        espressoVersion = '3.0.1'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {

    if (project.name != 'app') {
        apply plugin: 'digital.wup.android-maven-publish'
        publishing {
            publications {
                mavenAar(MavenPublication) {
                    groupId 'com.github.wordpress-mobile.WordPress-Aztec-Android'
                    artifactId project.name
                    version '123.456.789'
                    from components.android
                }
            }
        }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.9.2'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}
